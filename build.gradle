plugins {
    id 'org.openjfx.javafxplugin' version '0.0.14'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.lemaik.de/'
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    implementation.extendsFrom(provided)
    implementation.extendsFrom(bundled)
}

dependencies {
    provided 'se.llbit:chunky-core:2.5.0-SNAPSHOT'
    provided 'org.apache.commons:commons-math3:3.2'
    provided 'it.unimi.dsi:fastutil:8.4.4'
    provided 'se.llbit:jo-json:1.3.1'

    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    bundled 'commons-cli:commons-cli:1.5.0'
}

javafx {
    version = '17'
    modules = ['javafx.base', 'javafx.controls', 'javafx.fxml']
    configuration = 'provided'
}

tasks.register('pluginJar', Jar) {
    manifest {
        attributes "Main-Class": "dev.thatredox.chunky.animate.plugin.ChunkyAnimate"
    }

    from {
        configurations.bundled.collect { it.isDirectory() ? it : zipTree(it) }
    }

    with jar
}

tasks.register('standaloneJar', Jar) {
    archiveFileName = "ChunkyAnimate-Standalone.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes "Main-Class": "dev.thatredox.chunky.animate.standalone.AnimateLauncher"
    }

    from {
        configurations.bundled.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.provided.collect { it.isDirectory() ? it : zipTree(it) }
    }

    with jar
}

tasks.register('buildJar') {
    dependsOn pluginJar
    dependsOn standaloneJar
}
